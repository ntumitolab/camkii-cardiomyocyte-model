{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "309a4b34",
   "metadata": {},
   "source": [
    "# CaMKII system simplification and sensitivity analysis"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f05280e7",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ModelingToolkit\n",
    "using OrdinaryDiffEq, SteadyStateDiffEq, DiffEqCallbacks\n",
    "using Plots\n",
    "using LsqFit\n",
    "using CaMKIIModel\n",
    "using CaMKIIModel: μM, hil, second, Hz\n",
    "Plots.default(lw=1.5)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "90cef422",
   "metadata": {},
   "source": [
    "## CaMKII sensitivity to calcium\n",
    "\n",
    "Modeling CaM-Cax binding to CaMKII only. No phosphorylation or oxidation reactions."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "68109886",
   "metadata": {},
   "outputs": [],
   "source": [
    "@parameters Ca = 0μM ROS = 0μM\n",
    "sys = get_camkii_sys(; Ca, ROS, simplify=true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "def72a99",
   "metadata": {},
   "outputs": [],
   "source": [
    "prob = SteadyStateProblem(sys, [sys.k_phosCaM => 0])"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a801cc1c",
   "metadata": {},
   "source": [
    "Physiological cytosolic calcium concentrations ranges from 30nM to 10μM."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "273071c3",
   "metadata": {},
   "outputs": [],
   "source": [
    "ca = exp10.(range(log10(0.03μM), log10(10μM), 1001))\n",
    "prob_func = (prob, i, repeat) -> begin\n",
    "    remake(prob, p=[Ca => ca[i]])\n",
    "end\n",
    "trajectories = length(ca)\n",
    "alg = DynamicSS(FBDF())\n",
    "sol0 = solve(prob, alg; abstol=1e-10, reltol=1e-10) ## warmup\n",
    "@time sim = solve(EnsembleProblem(prob; prob_func, safetycopy=false), alg; trajectories, abstol=1e-10, reltol=1e-10)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "79902777",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"Extract values from ensemble simulations by a symbol\"\"\"\n",
    "extract(sim, k) = map(s -> s[k], sim)\n",
    "\"\"\"Calculate Root Mean Square Error (RMSE)\"\"\"\n",
    "rmse(fit) = sqrt(sum(abs2, fit.resid) / length(fit.resid))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cddfdab8",
   "metadata": {},
   "source": [
    "Status of the CaMKII system across a range of calcium concentrations."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9793ddf1",
   "metadata": {},
   "outputs": [],
   "source": [
    "xopts = (xlabel=\"Ca (μM)\", xscale=:log10, minorgrid=true, xlims=(ca[1], ca[end]))\n",
    "plot(ca, extract(sim, sys.CaM0), lab=\"CaM0\", ylabel=\"Conc. (μM)\"; xopts...)\n",
    "plot!(ca, extract(sim, sys.Ca2CaM_C), lab=\"Ca2CaM_C\")\n",
    "plot!(ca, extract(sim, sys.Ca2CaM_N), lab=\"Ca2CaM_N\")\n",
    "plot!(ca, extract(sim, sys.Ca4CaM), lab=\"Ca4CaM\")\n",
    "plot!(ca, extract(sim, sys.CaMK), lab=\"CaMK\")\n",
    "plot!(ca, extract(sim, sys.CaM0_CaMK), lab=\"CaM0_CaMK\")\n",
    "plot!(ca, extract(sim, sys.Ca2CaM_C_CaMK), lab=\"Ca2CaM_C_CaMK\")\n",
    "plot!(ca, extract(sim, sys.Ca2CaM_N_CaMK), lab=\"Ca2CaM_N_CaMK\")\n",
    "plot!(ca, extract(sim, sys.Ca4CaM_CaMK), lab=\"Ca4CaM_CaMK\", legend=:topleft)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b9facdfd",
   "metadata": {},
   "source": [
    "Active CaMKII is defined as CaMKII bound to CaM except CaM0 (apo CaM)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f0c3aa93",
   "metadata": {},
   "outputs": [],
   "source": [
    "CaMKAct = 1 - (sys.CaMK + sys.CaM0_CaMK) / sys.CAMKII_T\n",
    "println(\"Basal activity with 30nM Ca is \", sol0[CaMKAct][end])\n",
    "xdata = ca\n",
    "ydata = extract(sim, CaMKAct)\n",
    "plot(xdata, ydata, label=false, title=\"Active CaMKII\", ylims=(0, 0.5); xopts...)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1f216dc7",
   "metadata": {},
   "source": [
    "## Least-square fitting\n",
    "\n",
    "Least-square fitting of steady state CaMKII activities against calcium concentrations.\n",
    "\n",
    "Using the model:\n",
    "\n",
    "$$\n",
    "\\text{CaMKII}_{act} = p_1 \\frac{c^2}{c^2 + p_2^2} + p_3 \\frac{c^2}{c^2 + p_4^2} + p_5\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "22840a5e",
   "metadata": {},
   "outputs": [],
   "source": [
    "@. model_camk(x, p) = p[1] * hil(x, p[2], 2) + p[3] * hil(x, p[4], 4) + p[5]\n",
    "p0 = [0.4, 1μM, 0.0, 1μM, 0.0]\n",
    "lb = [0.0, 0.001μM, 0.0, 0.001μM, 0.0]\n",
    "@time fit = curve_fit(model_camk, xdata, ydata, p0; lower=lb, autodiff=:forwarddiff)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1e1919a7",
   "metadata": {},
   "source": [
    "Parameters"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "53562784",
   "metadata": {},
   "outputs": [],
   "source": [
    "pestim = coef(fit)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "54c12837",
   "metadata": {},
   "outputs": [],
   "source": [
    "println(\"Basal activity: \", pestim[5])\n",
    "println(\"Maximal activity by 2 Ca binding): \", pestim[1])\n",
    "println(\"Half saturation concentration for 2 Ca binding: \", pestim[2], \" μM\")\n",
    "println(\"Maximal activity by 4 Ca binding): \", pestim[3])\n",
    "println(\"Half saturation concentration for 4 Ca binding: \", pestim[4], \" μM\")\n",
    "println(\"RMSE: \", rmse(fit))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7e663feb",
   "metadata": {},
   "source": [
    "Visualize fitting"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b4976aef",
   "metadata": {},
   "outputs": [],
   "source": [
    "yestim = model_camk.(xdata, Ref(pestim))\n",
    "p1 = plot(xdata, [ydata yestim], lab=[\"Full model\" \"Fitted\"], line=[:dash :dot], title=\"CaMKII activity\", legend=:topleft; xopts...)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.2",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
